#!/usr/bin/env python
import os
import sys

env = SConscript("godot-cpp/SConstruct")

output_dir = "../addons/gd_chess_cpp/bin/"

# For reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

# tweak this if you want to use different folders, or more folders, to store your source code in.
env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")

if env["target"] == "template_debug":
    env.Append(CXXFLAGS=["-DDEBUG"])
print("CXXFLAGS:", env['CXXFLAGS'])

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "{}libgd_chess.{}.{}.framework/libgd_chess.{}.{}".format(
            output_dir, env["platform"], env["target"], env["platform"], env["target"]
        ),
        source=sources,
    )
elif env["platform"] == "ios":
    if env["ios_simulator"]:
        library = env.StaticLibrary(
            "{}libgd_chess.{}.{}.simulator.a".format(output_dir, env["platform"], env["target"]),
            source=sources,
        )
    else:
        library = env.StaticLibrary(
            "{}libgd_chess.{}.{}.a".format(output_dir, env["platform"], env["target"]),
            source=sources,
        )
else:
    library = env.SharedLibrary(
        "{}libgd_chess{}{}".format(output_dir, env["suffix"], env["SHLIBSUFFIX"]),
        source=sources,
    )

Default(library)
